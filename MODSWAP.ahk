;======[ Crappy Generals Online Mod Loader]======;
;                                                                                         .           
; +++++++++++++++++++++====+++++====++++++++++++++++++++++++++=++++++++*++++++++++=====++=+++=++++++ 
; +++++++++++++++++++++=+++++====+==========+===++====++=====++++++++++==+++++++==+=+========+==++++ 
; ++++++++++++=+++++++++++++================+==================+++++++++++++=+====+==+=+++++=+++++=+ 
; ++=+++++++++++++++++============================-==============+++++=+++=======++==========+=+++++ 
; +*+++=++++++++++++++++==============++++=+====+==+=========+++++++++++++++=================++++++= 
; +++++++++++++++++++++===::-========:.:-=========-:-=======--:-=====+=====-===============+++++++++.
; +++++++++++++++++++=====*@:-======--#=.-=+==+=+:.@+:====+=--%=-=====--=+++===========-===++=++++++ 
; ++=+++++++++++++===++====@@ .=====-:@@. ....  . .@::-==---.%@--=-==::*=:======-==--==+++==++++++=+ 
; ++++++==++=+=++-+=..=+==.=@@ .-:     =%:=:-===++@@  .      *+ .-=+-.@@----===========++++++=++++++ 
; +++=====++=+==+-*@@=.=+=-  @@   %@@@@@@@@@@@@@@:.:*@@@@@@@@@@*+-.  =@. ----------=====++=+++++++++ 
; +++++=+=======+=:.#@# .::  .@@@@@@%@%#####*#%###%@+..........-*@@@@@*    ..  :=--===+==+=++=++++++ 
; ==+++++++++==-=. -..@@#  @@:.+*::=:......:+-:........::-------:.....-@@@#  #@@#===-+======++++++++ 
; ==+++*++++++=+@@* ..  %@@@@@@+:::::------=**##@@@@@@@@-...:-----=---:..:#@@=   :=+=-.-+===+++=++++ 
; =----====++==:.=@@%  @@@*.....----====---::::....   .-%@@%...::::---==--...@@*..:: -@@%-=++=+=++++ 
; =--:.  .-=+*++- . =#@%  ...:..:---------:.....:+*%@@@%#*%@@@%#+=-.....::--...@@#  @@*.:====+++=+== 
; +==-+@@=     ..-: .@@#%@@@@@%*...........=@@@@@@#*+-:::...:=*#%@@@@=+=. ..:-:.:@@@# .-==+=====++++ 
; ====:   :%@@@#=- *@@.......:-*%@@@@@@@@@@@....................... @@@@@@@  .-::.:@@- :===:.:-=+=== 
; ++++=-*@@@ :#@@:@@. :%*:.      @@%@%...:@@ .:-:=*@@@@=:-+#*+*%*. .@*   =@@@- .....:@@    +@@#=+++= 
; +====:%@@@     .@..@@  @@@@@@@@+.......=%@@- . #@%.  :@@@@@@+=@%%@@.:-:.  #@@@+..:..@@ +@@+.:====+ 
; +=++=-  *@@   -@. .=#%%#   %@....::..:+#%#%@@@@%#@@@@@@%++*@@@@%+..:-==--..  @@@+....@@%:.:-===+== 
; ==-:.::. +@@+=@@@@@@#=.%@@.   ..-=.+=.    ...-@* ................:-===--=---:..-@@%. :@  -=-===+++ 
; +=%@@#.    -@@%......*@@  @@@@@+:=-@@@@@@@:   @#.-=----=--=-==================-...@@# @@ +=-====== 
; +=-:=@@@@#+@@...----::.@@@#++=..--:.. ..#@@@@@@=:-=======-==---======-=-======---:..@@@@  :=====+= 
; ===:     .@@..==-===-:...=***=.-====-.....--:...-========-==================--===-=:. @@%@%:-=====.
; ==-+@@.  %@..-==========:....:-======-=--:::--=-====================================:.@@..--====== 
; =-:..#@@@@..-==-============-=====================================================-=-.@@ .====-=== 
; -===-  .@*.---======================-=============================================-=-.@@ :*======= 
; ====-..@-.:-========-==-=================----==================-==================-=-.@*     :===- 
; :     -@..==-===--=-:..:--====-=============================================-=====-=:.@@@@@@%---== 
; +@@@@@*@@:.:=====--:.@@:-==========----=====================-:..-=-=================..@@     :--=- 
; -.      #@...:-===-.#@@.====--===--======================----+@*.+-===============--.=@@ .::--=--- 
; ====---. @@@-.:==-.%@-..------============---=---=----:....:::@@.----=============-:.=@@    .:--=- 
; ==:.:.-==@@@@..--::@+ *+:.............................-#@@@#..=@*--========-======:..@@@@@@@=:--=- 
; ==*@@@@@: *@@@..=.:@+.#@@@@@@@@@*+:.-+=+#+-+**%@@@@@@@@%=....-@@.-=========-=====:..@@     ::-==== 
; ==-.    .  :@@# .:.@@.......-#@@@@@@@@@@@@@@@@@@%#-.....::...@@..--============-:. @@@ .==-======= 
; ==========.  @@@...#@=-=---:.......... .  ........:-==-=-:.@@#..=+===========--:..@@@  :==+===++== 
; =========-.  =@@@...::==========---*@@@@@*-=============-=@@=.:-------=======-:..#@@%..======+===+.
; =====-:  .%@@. @@@...---==========--:...::-===--=-======-**:.:--.....:===-=--...@@@*   :-====+==== 
; ==-:..=%@@%:    @@@+.:--==-========---============--====--::--:..*@@%:===--.. @@@@  #@@-  :-====+= 
; -:-*@@@#: .-=+=  @@@. .....-======================-====--=--:. :@@@@.-==--. =@@@-  .  +@@@=:====== 
; *@@@+. .:-====. ++*@@= .@@...:---=========-==-========----:. -@@@+ .:--.. .@@@-   :---. :---+===== 
; -:...-=====-. =@@  @@@% +@@@. ...----======--=====--==-:.. .@@@= ..::.. =@@@- *@+  -==-:---=-=====.
; =========-. -@@- .  .@@@  +@@@@:   ...:::::---::--:...   @@@@  ..-:...@@@@*    .@@@. .--========== 
; =+======:.#@@= .=-=   @@@*   @@@@@@@+               :@@@@@@...::.. .@@@.    -=-:  #@@@=======-==== 
; ========+@@- .-===-::@  @@@.    .-@@@@@@@@@@@@@@@@@@@@@@......  .@@@@+#. .=-=--==-. :--=========== 
; ========:..:-===-- :@#   @@@@*..  ...  .   ...   .    .....  @@@@@     @@==========--============= 
;.+++===+=-=======-.#@# .:  .=@@@@@=.  ..............:...  .@@@@@ .   -:  .:--====================== 
; ======+==========@@+..---:   . .@@@@@@@@@@+.      ...*@@@@@%   @@= ::---=-=+=======+++++========== 
; =+==============-: .--=------..   .  -#@@@@@@@@@@@@@@@@..   ..:  @@ .-------================+=+=== 
; +======+==========--=-=-----==- %@: .                    .-=-==-. @@=---=---==============++====== 
; ============+=========-=====---.@* ---=--:-%%::---=:.@@==---=---=:...----=-=====================+= 
; ++++==+=========-==-====-==--==:-.=+=====--:.--===--: ..======-=--------==-========++=++++=====++= 
; ==++======================-=============================----===-=-=====--========================+ 
; ++=========================-=============================================================+++++==++ 
; +=+++++=========-===============================================================================+= 
; Main GUI for launching mods — AutoHotkey v2
#SingleInstance Force
#Requires AutoHotkey v2.0

IniFile       := A_ScriptDir "\CGOML Files\INI\mods.ini"
activeModIni  := A_ScriptDir "\CGOML Files\INI\active_mod.ini"
prefsFile     := A_ScriptDir "\CGOML Files\INI\Preferences.ini"
modsDir       := A_ScriptDir "\CGOML Files\Mods\"
wavFile       := A_ScriptDir "\CGOML Files\bog.wav"
buttonSound   := A_ScriptDir "\CGOML Files\button.wav"
successSound  := A_ScriptDir "\CGOML Files\incredible.wav"
failureSound  := A_ScriptDir "\CGOML Files\failure.wav"
runModExe     := A_ScriptDir "\CGOML Files\Sub Programs\RunMod.exe"
runModAhk     := A_ScriptDir "\CGOML Files\Sub Programs\RunMod.ahk"

; ==========================
; Load Preferences
; ==========================
DiagnosticsEnabled := IniRead(prefsFile, "Preferences", "Diagnostics", "0")
MusicEnabled       := IniRead(prefsFile, "Preferences", "Music", "1")
Theme              := IniRead(prefsFile, "Preferences", "Theme", "Light")

; ==========================
; Bog Player (.wav)
; ==========================
player  := ComObject("WMPlayer.OCX")
player.settings.setMode("loop", true)

if (MusicEnabled="1" && FileExist(wavFile)) {
    player.URL := wavFile
    player.controls.play()
}

PlayMusic() {
    global player
    try player.controls.play()
}
PauseMusic() {
    global player
    try player.controls.pause()
}

; ==========================
; Theme System
; ==========================
ApplyTheme(targetGui, theme) {
    if (theme = "Dark") {
        targetGui.BackColor := "0x202020"
        for ctrl in targetGui {
            if (ctrl.Type = "Text")
                ctrl.SetFont("cWhite")
            else if (ctrl.Type = "Button" || ctrl.Type = "Checkbox" || ctrl.Type = "DropDownList")
                ctrl.SetFont("cWhite")
        }
    } else { ; Light theme
        targetGui.BackColor := "0xFFFFFF"
        for ctrl in targetGui {
            if (ctrl.Type = "Text")
                ctrl.SetFont("cBlack")
            else if (ctrl.Type = "Button" || ctrl.Type = "Checkbox" || ctrl.Type = "DropDownList")
                ctrl.SetFont("cBlack")
        }
    }
}

; ==========================
; Create Main GUI
; ==========================
CGOML := Gui("+Resize", "Mods GUI")
btnMap := Map()
btnList := Map()
statusLbl := ""
global clearBtn

; Function to build/rebuild the mod list
BuildModList() {
    global CGOML, IniFile, activeModIni, btnMap, btnList, DiagnosticsEnabled, statusLbl, MusicEnabled, Theme, modsDir, clearBtn

    ; Clear old controls
    for ctrl in CGOML
        ctrl.Destroy()

    yPos := 10
    btnMap := Map()
    btnList := Map()

    ; Load mods.ini
    sections := []
    if FileExist(IniFile) {
        content := FileRead(IniFile)
        for line in StrSplit(content, "`n") {
            line := Trim(line)
            m := []
            if RegExMatch(line, "^\[(.+)\]$", &m)
                sections.Push(m[1])
        }
    }

    ; Auto-detect unlisted mods (folders with .gib files)
    Loop Files, modsDir "*", "D" {
        if !FileExist(A_LoopFileFullPath "\*.gib")
            continue
        if !sections.Has(A_LoopFileName) {
            sections.Push(A_LoopFileName)
            IniWrite(A_LoopFileName, IniFile, A_LoopFileName, "name")
        }
    }

    if (DiagnosticsEnabled = "1")
        SoundPlay(successSound, "false")
        MsgBox("Detected " sections.Length " mods total")
        

    ; Read active mod
    activeMod := ""
    if FileExist(activeModIni)
        activeMod := IniRead(activeModIni, "State", "ActiveMod", "")

    ; Build buttons
    for headerName in sections {
        modName := IniRead(IniFile, headerName, "name", "")
        if (modName = "")
            continue

        btnOptions := "x10 y" yPos " w200 h30"
        if (modName = activeMod)
            btnOptions .= " BackgroundGreen"

        btn := CGOML.Add("Button", btnOptions, modName)
        btnMap[btn] := headerName
        btnList[headerName] := btn
        btn.OnEvent("Click", (thisBtn, *) => OnModClick(thisBtn, btnMap, btnList, activeModIni))

        CGOML.Add("Text", "x220 y" yPos " w300 h30", "Play " modName " on Generals Online")
        yPos += 40
    }

    ; Preferences button
    prefsBtn := CGOML.Add("Button", "x10 y" yPos+10 " w200 h30", "Preferences")
    prefsBtn.OnEvent("Click", OpenPreferences)

    ; Clear Active Mod button (disabled if no active mod)
    clearBtn := CGOML.Add("Button", "x10 y" yPos+50 " w200 h30", "Clear Active Mod")
    clearBtn.OnEvent("Click", ClearActiveMod)
    clearBtn.Enabled := (activeMod != "")

    ; Status indicator
    statusLbl := CGOML.Add("Text", "x220 y" yPos+15 " w400 h30"
        , "Active Mod: " . (activeMod="" ? "None" : activeMod))

    ; Apply theme
    ApplyTheme(CGOML, Theme)
}

; ==========================
; Preferences GUI
; ==========================
OpenPreferences(*) {
    global prefsFile, MusicEnabled, Theme, CGOML, PrefsGui

    PrefsGui := Gui("+Owner" CGOML.Hwnd, "Preferences")

    PrefsGui.Add("Checkbox", "vchkMusic", "Enable Background Music").Value := (MusicEnabled="1")

    PrefsGui.Add("Text", , "Theme:")
    ddl := PrefsGui.Add("DropDownList", "vddlTheme w100", ["Light","Dark"])
    ddl.Value := (Theme = "Dark" ? 2 : 1)

    PrefsGui.Add("Button", "Default w100", "Save").OnEvent("Click", (*) => SavePreferences(PrefsGui))

    ApplyTheme(PrefsGui, Theme)
    PrefsGui.Show()
}

SavePreferences(guiRef) {
    global prefsFile, player, wavFile, MusicEnabled, Theme, CGOML, statusLbl, activeModIni

    controls := guiRef.Submit()

    ; Save Music
    MusicEnabled := controls.chkMusic ? "1" : "0"
    IniWrite(MusicEnabled, prefsFile, "Preferences", "Music")
    if (MusicEnabled="1" && FileExist(wavFile)) {
        player.URL := wavFile
        player.controls.play()
    } else {
        player.controls.stop()
    }

    ; Save Theme
    Theme := controls.ddlTheme
    IniWrite(Theme, prefsFile, "Preferences", "Theme")

    ; Apply theme live
    ApplyTheme(CGOML, Theme)
    ApplyTheme(guiRef, Theme)

    ; Update status indicator
    activeMod := ""
    if FileExist(activeModIni)
        activeMod := IniRead(activeModIni, "State", "ActiveMod", "")
    statusLbl.Text := "Active Mod: " . (activeMod="" ? "None" : activeMod)
}

; ==========================
; Detect focus changes
; ==========================
SetTimer(CheckFocus, 500)
CheckFocus() {
    global CGOML, PrefsGui, player, MusicEnabled
    hwnd := WinActive("A")
    if (MusicEnabled != "1")
        return
    if (hwnd = CGOML.Hwnd || (IsSet(PrefsGui) && hwnd = PrefsGui.Hwnd)) {
        if (player.playState != 3)
            PlayMusic()
    } else {
        if (player.playState = 3)
            PauseMusic()
    }
}

; ==========================
; Copy Mod Files
; ==========================
CopyModFiles(srcDir, destDir) {
    Loop Files srcDir "\*.*", "R" {
        srcFile := A_LoopFileFullPath
        relPath := SubStr(srcFile, StrLen(srcDir) + 2)
        destFile := destDir "\" relPath

        SplitPath(destFile, , &destFolder)
        if !DirExist(destFolder)
            DirCreate(destFolder)

        ToolTip("Copying: " relPath)
        Sleep(50)

        FileCopy(srcFile, destFile, true)

        ; Handle .gib → .big rename
        if RegExMatch(destFile, "\.gib$") {
            newFile := RegExReplace(destFile, "\.gib$", ".big")
            FileMove(destFile, newFile, true)
            ToolTip("Renamed: " relPath " → " SubStr(newFile, StrLen(destDir) + 2))
            Sleep(50)
        }
    }
    ToolTip()
}

; ==========================
; Button click handler
; ==========================
OnModClick(btn, btnMap, btnList, activeModIni) {
    global runModExe, runModAhk, buttonSound, statusLbl, modsDir, A_ScriptDir, clearBtn

    headerName := btnMap[btn]

    ; Play button sound
    if FileExist(buttonSound) {
        SoundPlay(buttonSound, "wait")
    }

    ; Copy mod files before running
    modPath := modsDir headerName
    if DirExist(modPath)
        CopyModFiles(modPath, A_ScriptDir)

    if FileExist(runModExe) {
        Run('"' runModExe '" "' headerName '"')
    } else if FileExist(runModAhk) {
        Run('"' runModAhk '" "' headerName '"')
    } else {
        MsgBox("Neither RunMod.exe nor RunMod.ahk could be found in Sub Programs folder.")
        return
    }

    for name, button in btnList
        button.Opt("BackgroundDefault")
    btn.Opt("BackgroundGreen")

    activeMod := IniRead(A_ScriptDir "\CGOML Files\INI\mods.ini", headerName, "name", "")
    IniWrite(activeMod, activeModIni, "State", "ActiveMod")

    statusLbl.Text := "Active Mod: " . activeMod
    clearBtn.Enabled := true
}

; ==========================
; Clear Active Mod Files
; ==========================
ClearActiveMod(*) {
    global activeModIni, modsDir, A_ScriptDir, btnList, statusLbl, clearBtn

    activeMod := ""
    if FileExist(activeModIni)
        activeMod := IniRead(activeModIni, "State", "ActiveMod", "")

    if (activeMod = "" || activeMod = " ") {
        statusLbl.Text := "Active Mod: None"
        clearBtn.Enabled := false
        return
    }

    confirm := MsgBox("Do you want to clear the active mod: " activeMod "?" 
        . "`nThis will remove its loaded .big files from the game folder.",
        "Confirm", "YesNo ")

    if (confirm != "Yes") {
        return
    }

    gameDir := A_ScriptDir
    modPath := modsDir activeMod

    ; Walk through the active mod's files
    Loop Files modPath "\*.*", "R" {
        relPath := SubStr(A_LoopFileFullPath, StrLen(modPath) + 2)
        targetFile := gameDir "\" relPath

        if FileExist(targetFile) {
            ToolTip("Deleting: " relPath)
            Sleep(50)
            try FileDelete(targetFile)
        }
        ; Also handle leftover .gib → .big cases
        else if RegExMatch(relPath, "\.gib$") {
            altTarget := gameDir "\" RegExReplace(relPath, "\.gib$", ".big")
            if FileExist(altTarget) {
                ToolTip("Deleting leftover: " SubStr(altTarget, StrLen(gameDir) + 2))
                Sleep(50)
                try FileDelete(altTarget)
            }
        }
    }
    ToolTip()

    ; Clear active mod in INI
    IniWrite("", activeModIni, "State", "ActiveMod")

    ; Reset button highlighting
    for name, button in btnList
        button.Opt("BackgroundDefault")

    ; Update status indicator
    statusLbl.Text := "Active Mod: None"

    ; Disable Clear button since no mod is active
    clearBtn.Enabled := false
}

; ==========================
; Exit handling
; ==========================
CGOML.OnEvent("Close", (*) => ExitApp())
OnExit(ExitCleanup)

ExitCleanup(*) {
    global player
    try player.controls.stop()
}

; ==========================
; Startup
; ==========================
BuildModList()
CGOML.Show()
Return